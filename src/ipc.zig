const std = @import("std");
const posix = std.posix;

pub const Tag = enum(u8) {
    Input = 0,
    Output = 1,
    Resize = 2,
    Detach = 3,
    DetachAll = 4,
    Kill = 5,
    Info = 6,
    Init = 7,
    History = 8,
    Run = 9,
    Ack = 10,
    SessionEnd = 11,
    Snapshot = 12,
    ReliableReplay = 13,
    CandidateRefresh = 14,
    TransportSwitchRequest = 15,
    TransportSwitchAck = 16,
};

pub const Header = packed struct {
    tag: Tag,
    len: u32,
};

pub const Resize = packed struct {
    rows: u16,
    cols: u16,
};

pub const Init = packed struct {
    rows: u16,
    cols: u16,
    snapshot_id: u32,
};

pub const Snapshot = packed struct {
    id: u32,
    flags: u8,

    pub fn isFinal(self: Snapshot) bool {
        return (self.flags & 0x1) != 0;
    }
};

pub const ReliableReplay = packed struct {
    seq: u32,
    channel: u8,
};

pub const TransportMode = enum(u8) {
    udp = 0,
    ssh = 1,
};

pub const TransportSwitchRequest = packed struct {
    mode: TransportMode,
};

pub const TransportSwitchAck = packed struct {
    mode: TransportMode,
    baseline_snapshot_id: u32,
};

pub const MAX_CMD_LEN = 256;
pub const MAX_CWD_LEN = 256;

pub const Info = extern struct {
    clients_len: usize,
    pid: i32,
    cmd_len: u16,
    cwd_len: u16,
    cmd: [MAX_CMD_LEN]u8,
    cwd: [MAX_CWD_LEN]u8,
    created_at: u64,
    task_ended_at: u64,
    task_exit_code: u8,
};

pub fn expectedLength(data: []const u8) ?usize {
    if (data.len < @sizeOf(Header)) return null;
    const header = std.mem.bytesToValue(Header, data[0..@sizeOf(Header)]);
    return @sizeOf(Header) + header.len;
}

pub fn send(fd: i32, tag: Tag, data: []const u8) !void {
    const header = Header{
        .tag = tag,
        .len = @intCast(data.len),
    };
    const header_bytes = std.mem.asBytes(&header);
    try writeAll(fd, header_bytes);
    if (data.len > 0) {
        try writeAll(fd, data);
    }
}

pub fn appendMessage(alloc: std.mem.Allocator, list: *std.ArrayList(u8), tag: Tag, data: []const u8) !void {
    const header = Header{
        .tag = tag,
        .len = @intCast(data.len),
    };
    try list.appendSlice(alloc, std.mem.asBytes(&header));
    if (data.len > 0) {
        try list.appendSlice(alloc, data);
    }
}

pub fn appendReliableReplay(alloc: std.mem.Allocator, list: *std.ArrayList(u8), seq: u32, channel: u8, data: []const u8) !void {
    const len = @sizeOf(ReliableReplay) + data.len;
    const payload = try alloc.alloc(u8, len);
    defer alloc.free(payload);

    const replay = ReliableReplay{ .seq = seq, .channel = channel };
    @memcpy(payload[0..@sizeOf(ReliableReplay)], std.mem.asBytes(&replay));
    @memcpy(payload[@sizeOf(ReliableReplay)..], data);
    try appendMessage(alloc, list, .ReliableReplay, payload);
}

pub fn parseReliableReplay(data: []const u8) !struct { seq: u32, channel: u8, payload: []const u8 } {
    if (data.len < @sizeOf(ReliableReplay)) return error.InvalidReliableReplay;
    const replay = std.mem.bytesToValue(ReliableReplay, data[0..@sizeOf(ReliableReplay)]);
    return .{
        .seq = replay.seq,
        .channel = replay.channel,
        .payload = data[@sizeOf(ReliableReplay)..],
    };
}

pub fn encodeTransportSwitchRequest(out: *[8]u8, mode: TransportMode) []const u8 {
    const request = TransportSwitchRequest{ .mode = mode };
    const bytes = std.mem.asBytes(&request);
    @memcpy(out[0..bytes.len], bytes);
    return out[0..bytes.len];
}

pub fn parseTransportSwitchRequest(data: []const u8) !TransportSwitchRequest {
    if (data.len != @sizeOf(TransportSwitchRequest)) return error.InvalidTransportSwitchRequest;
    return std.mem.bytesToValue(TransportSwitchRequest, data);
}

pub fn encodeTransportSwitchAck(out: *[8]u8, mode: TransportMode, baseline_snapshot_id: u32) []const u8 {
    const ack = TransportSwitchAck{ .mode = mode, .baseline_snapshot_id = baseline_snapshot_id };
    const bytes = std.mem.asBytes(&ack);
    @memcpy(out[0..bytes.len], bytes);
    return out[0..bytes.len];
}

pub fn parseTransportSwitchAck(data: []const u8) !TransportSwitchAck {
    if (data.len != @sizeOf(TransportSwitchAck)) return error.InvalidTransportSwitchAck;
    return std.mem.bytesToValue(TransportSwitchAck, data);
}

fn writeAll(fd: i32, data: []const u8) !void {
    var index: usize = 0;
    while (index < data.len) {
        const n = try posix.write(fd, data[index..]);
        if (n == 0) return error.DiskQuota;
        index += n;
    }
}

pub const Message = struct {
    tag: Tag,
    data: []u8,

    pub fn deinit(self: Message, alloc: std.mem.Allocator) void {
        if (self.data.len > 0) {
            alloc.free(self.data);
        }
    }
};

pub const SocketMsg = struct {
    header: Header,
    payload: []const u8,
};

pub const SocketBuffer = struct {
    buf: std.ArrayList(u8),
    alloc: std.mem.Allocator,
    head: usize,

    pub fn init(alloc: std.mem.Allocator) !SocketBuffer {
        return .{
            .buf = try std.ArrayList(u8).initCapacity(alloc, 4096),
            .alloc = alloc,
            .head = 0,
        };
    }

    pub fn deinit(self: *SocketBuffer) void {
        self.buf.deinit(self.alloc);
    }

    /// Reads from fd into buffer.
    /// Returns number of bytes read.
    /// Propagates error.WouldBlock and other errors to caller.
    /// Returns 0 on EOF.
    pub fn read(self: *SocketBuffer, fd: i32) !usize {
        if (self.head > 0) {
            const remaining = self.buf.items.len - self.head;
            if (remaining > 0) {
                std.mem.copyForwards(u8, self.buf.items[0..remaining], self.buf.items[self.head..]);
                self.buf.items.len = remaining;
            } else {
                self.buf.clearRetainingCapacity();
            }
            self.head = 0;
        }

        var tmp: [4096]u8 = undefined;
        const n = try posix.read(fd, &tmp);
        if (n > 0) {
            try self.buf.appendSlice(self.alloc, tmp[0..n]);
        }
        return n;
    }

    /// Returns the next complete message or `null` when none available.
    /// `buf` is advanced automatically; caller keeps the returned slices
    /// valid until the following `next()` (or `deinit`).
    pub fn next(self: *SocketBuffer) ?SocketMsg {
        const available = self.buf.items[self.head..];
        const total = expectedLength(available) orelse return null;
        if (available.len < total) return null;

        const hdr = std.mem.bytesToValue(Header, available[0..@sizeOf(Header)]);
        const pay = available[@sizeOf(Header)..total];

        self.head += total;
        return .{ .header = hdr, .payload = pay };
    }
};

test "transport switch payload round trip" {
    var request_buf: [8]u8 = undefined;
    const request_payload = encodeTransportSwitchRequest(&request_buf, .udp);
    const request = try parseTransportSwitchRequest(request_payload);
    try std.testing.expect(request.mode == .udp);

    var ack_buf: [8]u8 = undefined;
    const ack_payload = encodeTransportSwitchAck(&ack_buf, .udp, 42);
    const ack = try parseTransportSwitchAck(ack_payload);
    try std.testing.expect(ack.mode == .udp);
    try std.testing.expectEqual(@as(u32, 42), ack.baseline_snapshot_id);
}
